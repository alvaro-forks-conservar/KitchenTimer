buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    def VERSION_NAME_EXTENDED = VERSION_NAME + " build " + VERSION_CODE

    defaultConfig {
        applicationId "com.leinardi.kitchentimer"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName VERSION_NAME_EXTENDED
    }

    productFlavors {
        standard {
            applicationId "com.leinardi.kitchentimer"
        }

        foss {
            applicationId "com.leinardi.kitchentimer.foss"
            versionName VERSION_NAME_EXTENDED + "-foss"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def isKeystoreInfoAvailable = file(KEYSTORE_PATH).exists() && file(KEYSTORE_PROPERTIES_PATH).exists()

    signingConfigs {
        if (isKeystoreInfoAvailable) {
            release {
                Properties props = new Properties()
                props.load(new FileInputStream(file(KEYSTORE_PROPERTIES_PATH)))
                storeFile file(KEYSTORE_PATH)
                storePassword props['store.password']
                keyAlias props['key.alias']
                keyPassword props['key.password']
            }
        }
    }

    buildTypes {
        if (isKeystoreInfoAvailable) {
            release {
                minifyEnabled true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    // It would be better to fix the issues
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:22.1.1'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:recyclerview-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.1.1'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'jp.wasabeef:recyclerview-animators:1.1.3@aar'
    compile 'io.realm:realm-android:0.80.1'
    compile 'com.nispok:snackbar:2.10.7'
    compile "com.daimajia.swipelayout:library:1.2.0@aar"
    compile 'com.github.gabrielemariotti.changeloglib:library:1.5.2'

    standardCompile('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') {
        transitive = true;
    }
}
